# Maintainer: kotontrion <kotontrion@tutanota.de>

pkgbase=kompass-git
pkgname=(libkompass-git kompass-git)
pkgver=r105.9e02183
pkgrel=1
pkgdesc="kotontrions personal status panel"
arch=(x86_64)
license=(GPL-3.0-only)
url="https://github.com/kotontrion/kompass"
depends=(
  gtk4
  gtk4-layer-shell
  glib2
  libadwaita
  libnm
  libastal-meta
  libportal
  slurp
  wf-recorder
)
makedepends=(
  meson
  blueprint-compiler
  dart-sass
  git
  gobject-introspection
  vala
)
provides=(kompass libkompass)
source=("${pkgname}::git+https://github.com/kotontrion/kompass.git")
sha256sums=('SKIP')

pkgver() {
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "$pkgname"
  arch-meson build
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
    done
}

package_libkompass-git() {

  provides=(libkompass.so)

  cd "$pkgname"
  meson install -C build --destdir "$pkgdir"
  
  cd "$pkgdir"

  _pick kompass-bar usr/bin/kompass
}

package_kompass-git() {
  depends+=(libkompass-git)
  mv kompass-bar/* "$pkgdir"
}
